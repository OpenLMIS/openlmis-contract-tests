apply plugin: 'java'
apply plugin: 'idea'

dependencies {
    compile 'org.postgresql:postgresql:9.4.1211'
    compile 'com.ibatis:ibatis2-common:2.1.7.597'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'org.json:json:20170516'
    compile 'org.yaml:snakeyaml:1.18'
    compile 'org.projectlombok:lombok:1.18.0'

    testCompile 'io.rest-assured:rest-assured:3.0.0'
    testCompile 'io.cucumber:cucumber-java:3.0.2'
    testCompile 'io.cucumber:cucumber-junit:3.0.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.jglue.fluent-json:fluent-json:2.0.0'
}

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java {
            srcDir 'src/cucumber/java'
        }
        resources {
            srcDir 'src/cucumber/resources'
        }

        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

task cucumber(dependsOn: testClasses) {
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.testRuntimeClasspath + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--add-plugin', 'pretty',
                    '--add-plugin', 'html:build/cucumber',
                    '--add-plugin', 'junit:build/cucumber/junit/test_report.xml',
                    '--add-plugin', 'json:build/cucumber.json',
                    '--glue', 'classpath:org.openlmis.contract_tests',
                    '--monochrome',
                    '--tags', project.getProperty('tags'),
                    'src/cucumber/resources/']
        }
    }
}

task waitFor {
    if (project.hasProperty('containers')) {
        def containers = project.getProperty('containers').split(',')
        println "waiting for " + containers + " to be started up and serving"

        for (container in containers) {
            if (!isReachable(container)) {
                throw new GradleException('timed out waiting for ' + container)
            }
        }
    }
}

private boolean isReachable(container) {
    boolean isRequestSuccessFul = false

    for (def i = 0; i < 50; i++) {
        println("trying $container ${i + 1} times")

        def response = ""
        try {
            response = new URL(container).text
            println response
        } catch (Exception e) {
            println "request failed"
        }

        if (response != "") {
            isRequestSuccessFul = true;
            println "request successful, waiting 5 more seconds for right assignment regeneration"
            sleep(5000)
            break
        } else {
            sleep(5000)
        }
    }

    return isRequestSuccessFul
}
